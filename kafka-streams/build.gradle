plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.2'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://packages.confluent.io/maven/'
    }
}

String grpcVersion = '1.54.0'
String protoVersion = '3.22.2'
String lombokVersion = '1.18.26'

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    implementation project(':kafka-avro')
    implementation 'org.apache.kafka:kafka-streams:3.3.2'
    implementation 'io.confluent:kafka-streams-avro-serde:7.3.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    implementation 'info.picocli:picocli:4.6.1'
    implementation 'ch.qos.logback:logback-classic:1.4.6'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protoVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc { } }
    }
}

application {
    mainClass = 'kafka.sandbox.App'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
